//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
    dependencies {
        classpath "com.modrinth.minotaur:Minotaur:${libs.versions.minotaur.get()}"
        classpath "com.github.breadmoirai:github-release:${libs.versions.githubRelease.get()}"
    }
}

plugins {
    id 'maven-publish'
    alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = project.name
group = 'net.modfest'

repositories {
    maven { url 'https://maven.terraformersmc.com/releases' }
    maven { url 'https://maven.gegy.dev' }
    maven { url 'https://maven.quiltmc.org/repository/release' }
    mavenLocal()
}

dependencies {
    minecraft libs.mc
    mappings variantOf(libs.qm) { classifier "intermediary-v2" }
    modImplementation libs.fl
    modImplementation libs.fapi
    modImplementation libs.spruceui

    include libs.spruceui
}

processResources {
    final Map<String, String> map = [
        "version" : version.toString(),
        "mc"      : libs.versions.mc.get(),
        "fl"      : libs.versions.fl.get(),
        "fapi"    : libs.versions.fapi.get(),
        "spruceui": libs.versions.spruceui.get()
    ]

    inputs.properties(map)
    filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

if (System.getenv("MODRINTH_TOKEN")) {
    apply plugin: "com.modrinth.minotaur"
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = 'ballotbox'
        versionNumber = project.version
        versionName = baseVersion
        versionType = 'release'
        uploadFile = remapJar
        gameVersions = [libs.versions.mc.get()]
        loaders = ['fabric']
        detectLoaders = false
        dependencies {
            required.version libs.fapi.get().getName(), libs.versions.fapi.get()
            embedded.version libs.spruceui.get().getName(), libs.versions.spruceui.get()
        }
        changelog = rootProject.file("CHANGELOG.md").text + "\n\nFull Changelog: https://github.com/modfest/ballotbox/releases/tag/v$baseVersion"
        syncBodyFrom = rootProject.file("README.md").text
    }
}

if (System.getenv("GITHUB_TOKEN")) {
    apply plugin: "com.github.breadmoirai.github-release"
    githubRelease {
        token = System.getenv("GITHUB_TOKEN")
        owner = "modfest"
        repo = "ballotbox"
        tagName = "v$baseVersion"
        releaseName = baseVersion
        targetCommitish = defaultBranch
        draft = false
        prerelease = false
        releaseAssets = remapJar.archiveFile
        allowUploadToExisting = true
        generateReleaseNotes = true
        body = rootProject.file("CHANGELOG.md").text + "\n"
    }
    tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
    tasks.register("fullRelease") {
        group "publishing"
        dependsOn "githubRelease"
        dependsOn "modrinth"
    }
}
