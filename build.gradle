//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
	id "maven-publish"
	alias libs.plugins.loom
	alias libs.plugins.minotaur
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = project.archivesName

repositories {
	maven { url "https://maven.terraformersmc.com/releases" }
	maven { url "https://maven.gegy.dev" }
	maven { url "https://maven.quiltmc.org/repository/release" }
	maven { url "https://repo.sleeping.town/" }
}

dependencies {
	minecraft libs.mc
	mappings variantOf(libs.yarn) { classifier "v2" }
	modImplementation libs.fl
	modImplementation libs.fapi
	modImplementation libs.spruceui
	implementation libs.kaleidoConfig

	include libs.spruceui
	include libs.kaleidoConfig
}

processResources {
	final Map<String, String> meta = [
		version       : version,
		modId         : modId,
		modName       : modName,
		modDescription: modDescription,
		homepage      : homepage,
		issues        : issues,
		sources       : sources,
		license       : license,
		authors       : authors.split(", ").join("\",\n    \""),
		contributors  : contributors.split(", ").join("\",\n    \""),
		mc            : libs.versions.mc.get(),
		fl            : libs.versions.fl.get(),
		fapi          : libs.versions.fapi.get(),
		spruceui      : libs.versions.spruceui.get()
	]
	inputs.properties meta
	filesMatching("fabric.mod.json") { expand meta }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "ballotbox"
	dependencies {
		required.version "fabric-api", libs.versions.fapi.get()
	}
	changelog = System.getenv("CHANGELOG")
	syncBodyFrom = "<!--DO NOT EDIT MANUALLY: synced from gh readme-->\n" + rootProject.file("README.md").text
	uploadFile = remapJar
}
